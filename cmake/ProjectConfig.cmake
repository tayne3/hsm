include(CheckCCompilerFlag)
include(CheckCXXCompilerFlag)

set(CMAKE_C_STANDARD 99)
set(CMAKE_C_STANDARD_REQUIRED ON)
set(CMAKE_C_EXTENSIONS ON)
set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS ON)
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)
set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

include(GNUInstallDirs)
set(CMAKE_SKIP_BUILD_RPATH OFF)
set(CMAKE_BUILD_WITH_INSTALL_RPATH ON)
set(CMAKE_INSTALL_RPATH_USE_LINK_PATH OFF)
if(APPLE)
  set(CMAKE_MACOSX_RPATH ON)
  set(CMAKE_INSTALL_RPATH "@loader_path;@loader_path/${CMAKE_INSTALL_LIBDIR}")
else()
  set(CMAKE_INSTALL_RPATH "$ORIGIN;$ORIGIN/${CMAKE_INSTALL_LIBDIR}")
endif()

add_library(smf_compile_dependency INTERFACE)
target_compile_features(smf_compile_dependency INTERFACE c_std_99)
if(UNIX)
  target_link_libraries(smf_compile_dependency INTERFACE pthread)
endif()

# set source charset to utf-8 for MSVC
if(CMAKE_C_COMPILER_ID STREQUAL "MSVC")
  target_compile_options(smf_compile_dependency INTERFACE 
		"$<$<COMPILE_LANGUAGE:C>:/utf-8>"
	)
endif()
if(CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
  target_compile_options(smf_compile_dependency INTERFACE 
		"$<$<COMPILE_LANGUAGE:CXX>:/utf-8>"
	)
endif()

# compiler warnings, skipped for MSVC & ClangCL (MSVC frontend)
if(NOT (CMAKE_C_COMPILER_ID STREQUAL "MSVC" OR ("${CMAKE_C_COMPILER_ID}" STREQUAL "Clang" AND "${CMAKE_C_COMPILER_FRONTEND_VARIANT}" STREQUAL "MSVC")))
  target_compile_options(smf_compile_dependency INTERFACE
    "$<$<COMPILE_LANGUAGE:C>:-Wall>"
    "$<$<COMPILE_LANGUAGE:C>:-Wextra>"
  )
  check_c_compiler_flag("-Werror=return-type" HAVE_C_WERROR_RETURN_TYPE)
  if(HAVE_C_WERROR_RETURN_TYPE)
    target_compile_options(smf_compile_dependency INTERFACE
      "$<$<COMPILE_LANGUAGE:C>:-Werror=return-type>"
    )
  endif()
endif()
if(NOT (CMAKE_CXX_COMPILER_ID STREQUAL "MSVC" OR ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang" AND "${CMAKE_CXX_COMPILER_FRONTEND_VARIANT}" STREQUAL "MSVC")))
  target_compile_options(smf_compile_dependency INTERFACE
    "$<$<COMPILE_LANGUAGE:CXX>:-Wall>"
    "$<$<COMPILE_LANGUAGE:CXX>:-Wextra>"
  )
  check_cxx_compiler_flag("-Werror=return-type" HAVE_CXX_WERROR_RETURN_TYPE)
  if(HAVE_CXX_WERROR_RETURN_TYPE)
    target_compile_options(smf_compile_dependency INTERFACE
      "$<$<COMPILE_LANGUAGE:CXX>:-Werror=return-type>"
    )
  endif()
endif()

# Handle -Wno-missing-field-initializers for older GCC
if(CMAKE_C_COMPILER_ID STREQUAL "GNU" AND CMAKE_C_COMPILER_VERSION VERSION_LESS 6.0)
  target_compile_options(smf_compile_dependency INTERFACE 
		"$<$<COMPILE_LANGUAGE:C>:-Wno-missing-field-initializers>"
	)
endif()
if(CMAKE_CXX_COMPILER_ID STREQUAL "GNU" AND CMAKE_CXX_COMPILER_VERSION VERSION_LESS 6.0)
  target_compile_options(smf_compile_dependency INTERFACE 
		"$<$<COMPILE_LANGUAGE:CXX>:-Wno-missing-field-initializers>"
	)
endif()

if(CMAKE_C_COMPILER_ID STREQUAL "MSVC")
  target_compile_options(smf_compile_dependency INTERFACE 
		"$<$<COMPILE_LANGUAGE:C>:/GS>"
	)
  if(MSVC_VERSION GREATER_EQUAL 1920)
    target_compile_options(smf_compile_dependency INTERFACE 
		  "$<$<COMPILE_LANGUAGE:C>:/guard:cf>"
	  )
  endif()
endif()
if(CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
  target_compile_options(smf_compile_dependency INTERFACE 
		"$<$<COMPILE_LANGUAGE:CXX>:/GS>"
	)
  if(MSVC_VERSION GREATER_EQUAL 1920)
    target_compile_options(smf_compile_dependency INTERFACE 
		  "$<$<COMPILE_LANGUAGE:CXX>:/guard:cf>"
	  )
  endif()
endif()

if(CMAKE_C_COMPILER_ID MATCHES "GNU|Clang")
  check_c_compiler_flag("-fstack-protector-all" C_HAS_STACK_PROTECT_ALL)
  if(C_HAS_STACK_PROTECT_ALL)
    target_compile_options(smf_compile_dependency INTERFACE 
      "$<$<COMPILE_LANGUAGE:C>:-fstack-protector-all>"
    )
  else()
    check_c_compiler_flag("-fstack-protector-strong" C_HAS_STACK_PROTECT_STRONG)
    if(C_HAS_STACK_PROTECT_STRONG)
      target_compile_options(smf_compile_dependency INTERFACE 
      "$<$<COMPILE_LANGUAGE:C>:-fstack-protector-strong>"
      )
    else()
      check_c_compiler_flag("-fstack-protector" C_HAS_STACK_PROTECT)
      if(C_HAS_STACK_PROTECT)
        target_compile_options(smf_compile_dependency INTERFACE 
        "$<$<COMPILE_LANGUAGE:C>:-fstack-protector>"
        )
      endif()
    endif()
  endif()
endif()

if(CMAKE_CXX_COMPILER_ID MATCHES "GNU|Clang")
  check_cxx_compiler_flag("-fstack-protector-all" CXX_HAS_STACK_PROTECT_ALL)
  if(CXX_HAS_STACK_PROTECT_ALL)
    target_compile_options(smf_compile_dependency INTERFACE 
      "$<$<COMPILE_LANGUAGE:CXX>:-fstack-protector-all>"
    )
  else()
    check_cxx_compiler_flag("-fstack-protector-strong" CXX_HAS_STACK_PROTECT_STRONG)
    if(CXX_HAS_STACK_PROTECT_STRONG)
      target_compile_options(smf_compile_dependency INTERFACE 
      "$<$<COMPILE_LANGUAGE:CXX>:-fstack-protector-strong>"
      )
    else()
      check_cxx_compiler_flag("-fstack-protector" CXX_HAS_STACK_PROTECT)
      if(CXX_HAS_STACK_PROTECT)
        target_compile_options(smf_compile_dependency INTERFACE 
        "$<$<COMPILE_LANGUAGE:CXX>:-fstack-protector>"
        )
      endif()
    endif()
  endif()
endif()
